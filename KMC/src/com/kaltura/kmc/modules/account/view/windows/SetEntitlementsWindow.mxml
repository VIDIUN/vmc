<?xml version="1.0" encoding="utf-8"?>
<containers:HelpTitleWindow xmlns:mx="http://www.adobe.com/2006/mxml" xmlns:containers="com.vidiun.containers.*" 
							layout="vertical" width="400" height="300" creationComplete="creationCompleteHandler(event)" 
							showCloseButton="true" close="closeHandler(event)" showHelpButton="true" help="helpHandler(event)"
							xmlns:components="com.hillelcoren.components.*"
							title="{resourceManager.getString('account', 'setEntitlementsWindowTitle')}">
	<mx:Script>
		<![CDATA[
			import com.adobe.cairngorm.control.CairngormEvent;
			import com.vidiun.VidiunClient;
			import com.vidiun.autocomplete.controllers.VACCategoryController;
			import com.vidiun.autocomplete.itemRenderers.selection.CategorySelectedItem;
			import com.vidiun.vmc.business.JSGate;
			import com.vidiun.vmc.events.VmcHelpEvent;
			import com.vidiun.vmc.modules.account.control.events.IntegrationEvent;
			import com.vidiun.vmc.modules.account.model.AccountModelLocator;
			import com.vidiun.vo.VidiunCategory;
			
			import mx.controls.Alert;
			import mx.events.CloseEvent;
			import mx.events.FlexEvent;
			import mx.managers.PopUpManager;
			import mx.utils.StringUtil;

			
			private var _category:VidiunCategory;
			
			public function set category(value:VidiunCategory):void {
				_category = value;
				if (catComplete) {
					catComplete.selectedItem = value;
					catComplete.enabled = false;
					txt.text = value.privacyContext;
				}
			}
			
			private function creationCompleteHandler(event:FlexEvent):void {
				new VACCategoryController(catComplete, AccountModelLocator.getInstance().context.vc);
				if (_category) {
					category = _category;
				}
			}


			private function closeHandler(event:Event):void {
				PopUpManager.removePopUp(this);
				JSGate.maskHeader(true);
			}


			private function validate(event:MouseEvent):void {
				var contexts:String = getPrivacyContextString();
				var vCat:VidiunCategory = catComplete.selectedItem;
				if (!vCat) {
					Alert.show(resourceManager.getString('account', 'noCatSelected'), resourceManager.getString('account', 'error'));
				}
				else if (!_category && vCat.privacyContext) {
					Alert.show(resourceManager.getString('account', 'contextAlreadySet'), resourceManager.getString('account', 'error'));
				}
				else if (StringUtil.trim(contexts) == '') {
					Alert.show(resourceManager.getString('account', 'noContext'), resourceManager.getString('account', 'error'));
				}
				else if (!verifyFourChars(contexts)) {
					Alert.show(resourceManager.getString('account', 'entLabelShort'), resourceManager.getString('account', 'error'));
				}
				else {
					Alert.show(resourceManager.getString('account', 'setEntNote', [vCat.name]), resourceManager.getString('account', 'important'), Alert.OK|Alert.CANCEL, null, savePrivacyContext);
				}
			}
			
			private function verifyFourChars(contexts:String):Boolean {
				var ar:Array = contexts.split(',');
				for each (var str:String in ar) {
					if (str.length < 4) {
						return false;
					}
				}
				return true;
			}
			
			
			private function savePrivacyContext(e:CloseEvent):void {
				if (e.detail == Alert.OK) {
					var vCat:VidiunCategory = catComplete.selectedItem;
					vCat.privacyContext = getPrivacyContextString();
					var cge:CairngormEvent = new IntegrationEvent(IntegrationEvent.UPDATE_CATEGORY);
					cge.data = vCat;
					cge.dispatch();
					closeHandler(null);	
				}
			}
			
			private function getPrivacyContextString():String {
				return txt.text;
			}


			private function helpHandler(event:Event):void
			{
				dispatchEvent(new VmcHelpEvent(VmcHelpEvent.HELP, 'section_cat_entitlement'));
			}

		]]>
	</mx:Script>
	<mx:Label text="{resourceManager.getString('account', 'selectParentCat')}"/>
	<components:AutoComplete id="catComplete" width="100%" selectedItemStyleName="selectionBox"
							 showRemoveIcon="true" labelField="name"
							 selectionItemRendererClassFactory="{new ClassFactory(CategorySelectedItem)}" />
	<mx:Spacer height="20" />
	<mx:Label text="{resourceManager.getString('account', 'setPrivLabel')}"/>
	<mx:TextInput id="txt" width="100%" restrict="A-Za-z0-9,"/>
		
	<mx:ControlBar horizontalAlign="center">
		<mx:Button label="{resourceManager.getString('account', 'save')}" click="validate(event)"/>
		<mx:Button label="{resourceManager.getString('account', 'cancel')}" click="closeHandler(event)"/>
	</mx:ControlBar>
</containers:HelpTitleWindow>
