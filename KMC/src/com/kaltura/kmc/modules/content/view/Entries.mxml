<?xml version="1.0" encoding="utf-8"?>
<!---
	 "Manage" part of the content viewstack.
	 lists all entries, allows filtering, preview and drill-down to entry details.
-->
<mx:VBox xmlns:mx="http://www.adobe.com/2006/mxml" xmlns:controls="com.vidiun.controls.*"
		 implements="com.vidiun.vmc.modules.content.business.IContentPanel,com.vidiun.edw.business.IDataOwner"
		 creationComplete="creationCompleteHandler(event)" 
		 xmlns:view="com.vidiun.vmc.modules.content.view.*"
		 xmlns:business="com.vidiun.vmc.modules.content.business.*"
		 xmlns:et="com.vidiun.edw.components.et.*" 
		 xmlns:fltr="com.vidiun.edw.components.fltr.*" 
		 xmlns:indicators="com.vidiun.edw.components.fltr.indicators.*" 
		 xmlns:bulkactions="com.vidiun.vmc.modules.content.view.controls.bulkactions.*">

	<mx:Script>
		<![CDATA[
			import com.adobe.cairngorm.control.CairngormEvent;
			import com.vidiun.analytics.GoogleAnalyticsConsts;
			import com.vidiun.analytics.GoogleAnalyticsTracker;
			import com.vidiun.analytics.VAnalyticsTracker;
			import com.vidiun.analytics.VAnalyticsTrackerConsts;
			import com.vidiun.edw.business.permissions.PermissionManager;
			import com.vidiun.edw.components.et.EntryTableColumns;
			import com.vidiun.edw.control.VedController;
			import com.vidiun.edw.control.events.VedEntryEvent;
			import com.vidiun.edw.control.events.LoadEvent;
			import com.vidiun.edw.model.FilterModel;
			import com.vidiun.edw.model.datapacks.ContextDataPack;
			import com.vidiun.edw.model.datapacks.DistributionDataPack;
			import com.vidiun.edw.model.datapacks.EntryDataPack;
			import com.vidiun.edw.model.types.WindowsStates;
			import com.vidiun.edw.vo.ListableVo;
			import com.vidiun.events.FileUploadEvent;
			import com.vidiun.vmc.modules.content.events.CategoryEvent;
			import com.vidiun.vmc.modules.content.events.EntriesEvent;
			import com.vidiun.vmc.modules.content.events.VMCSearchEvent;
			import com.vidiun.vmc.modules.content.events.PartnerEvent;
			import com.vidiun.vmc.modules.content.events.SelectionEvent;
			import com.vidiun.vmc.modules.content.events.SetListableEvent;
			import com.vidiun.vmc.modules.content.events.SetPlaylistTypeEvent;
			import com.vidiun.vmc.modules.content.events.WindowEvent;
			import com.vidiun.vmvc.control.VMvCEvent;
			import com.vidiun.vmvc.model.VMvCModel;
			import com.vidiun.managers.FileUploadManager;
			import com.vidiun.types.VidiunMediaEntryOrderBy;
			import com.vidiun.types.VidiunStatsVmcEventType;
			import com.vidiun.vo.VidiunBaseEntryFilter;
			import com.vidiun.vo.VidiunMediaEntryFilter;
			
			import flash.utils.clearTimeout;
			import flash.utils.setTimeout;
			
			import mx.collections.ArrayCollection;
			import mx.controls.Alert;
			import mx.events.FlexEvent;

			[Bindable]
			/**
			 * show embed option in "publish" column
			 * */
			public var showEmbed:Boolean = true;

			/**
			 * interval for delaying loadEntries() calls
			 * */
			private var _loadEntriesInterval:int;

			[Bindable]
			private var _listableVo:ListableVo;

			/**
			 * data ordering
			 * */
			private var _orderBy:String = VidiunMediaEntryOrderBy.CREATED_AT_DESC;

			[Bindable]
			/**
			 * filtering data
			 * */
			public var filterModel:FilterModel;

			[Bindable]
			/**
			 * entries-related data
			 * */
			public var entryDetailsModel:VMvCModel;

			/**
			 * current window state
			 * */
			public var windowState:String;


			public function onRequestedDataLoaded():void {
				loadEntries();
			}


			/**
			 * initialize the panel.
			 * @param initFilter	filter for initial entry loading.
			 * */
			public function init(initFilter:VidiunBaseEntryFilter = null):void {
				paging.selectedPage = 1;
				paging.pageSize = paging.rowInPageCb.value as int;
				
				if (initFilter && initFilter is VidiunMediaEntryFilter) {
					filter.vidiunFilter = initFilter as VidiunMediaEntryFilter;
				}
				
				//When one click on switch tab and get back if the accordion filter is not the first child 
				//everything looked curapted. This will resolve the issue when one swith tabs
				/////////////////////////////////////////////////////////////////////////////////
				if(filter && filter.accordion)
					filter.accordion.selectedIndex = 0;
				/////////////////////////////////////////////////////////////////////////////////
				
				// load all the data the filter needs
				var fe:LoadEvent = new LoadEvent(LoadEvent.LOAD_FILTER_DATA, this, filterModel);
				VedController.getInstance().dispatch(fe);

				// get partner info
				var getPartnerInfoEvent:PartnerEvent = new PartnerEvent(PartnerEvent.GET_PARTNER_INFO);
				getPartnerInfoEvent.dispatch();

			}



			public function loadEntries():void {
				if (!searchBox.validateSearchText(true)) {
					return;
				}
				if (!_loadEntriesInterval) {
					_loadEntriesInterval = setTimeout(loadEntriesNow, 100);
				}
			}


			/**
			 * delay calls to loadEntries() to avoid redundant calls 
			 * (i.e., when uploading multiple files)
			 * */
			private function loadEntriesNow():void {
				clearTimeout(_loadEntriesInterval);
				_loadEntriesInterval = 0; // reset, so next call will trigger again
				_listableVo = new ListableVo(filter.vidiunFilter, paging, null);
				// set the order-by to the filter
				if (_orderBy) {
					_listableVo.filterVo.orderBy = _orderBy;
				}
				var cgEvent:CairngormEvent = new SetListableEvent(SetListableEvent.SET_NEW_LIST_TO_MODEL, _listableVo);
				cgEvent.dispatch();
				cgEvent = new VMCSearchEvent(VMCSearchEvent.DO_SEARCH_ENTRIES, _listableVo);
				cgEvent.dispatch();				
			}


			private function removeColums(event:Event):void {
				var ar:Array = [EntryTableColumns.THUMBNAIL, EntryTableColumns.ID, EntryTableColumns.NAME, 
					EntryTableColumns.MEDIA_TYPE, EntryTableColumns.PLAYS, EntryTableColumns.CREATE_AT,
					EntryTableColumns.DURATION, EntryTableColumns.STATUS, EntryTableColumns.ACTIONS];
				entryTable.showColumns(ar);
				// we always remove the thumbnail column, RnP will add it if required

				entryTable.updateDefaultSortColumn();
			}


			/**
			 * This is a new search called by an action on the filter vo
			 */
			private function newSearchFromFilter():void {
				paging.selectedPage = 1;
				loadEntries();
			}



			/**
			 * Create a new manual based playlist based on current selection
			 */
			private function onNewManualPlaylist():void {

				if (entryTable.selectedItems.length == 0) {
					Alert.show(resourceManager.getString('cms', 'pleaseSelectEntriesFirst'));
					return;
				}
				var vEvent:VMvCEvent = new VedEntryEvent(VedEntryEvent.SET_SELECTED_ENTRY, null);
				VedController.getInstance().dispatch(vEvent);

				// add the selected items to the model 
				var cgEvent:CairngormEvent = new EntriesEvent(EntriesEvent.SET_SELECTED_ENTRIES,
					new ArrayCollection(entryTable.selectedItems));
				cgEvent.dispatch();

				//tell the model that this is not a regular playlist - but a playlist with pre-defined entries
				var cgEvent2:CairngormEvent = new SetPlaylistTypeEvent(SetPlaylistTypeEvent.MANUAL_PLAYLIST);
				cgEvent2.dispatch();

				//open a new manual playlist window to edit this 
				cgEvent2 = new WindowEvent(WindowEvent.OPEN, WindowsStates.PLAYLIST_MANUAL_WINDOW);
				cgEvent2.dispatch();

			}

			/**
			 * Create a new category based on current entries selection
			 */
			private function onNewCategory():void {

				if (entryTable.selectedItems.length == 0) {
					Alert.show(resourceManager.getString('cms', 'pleaseSelectEntriesFirst'));
					return;
				}
				
				var cgEvent:CairngormEvent = new EntriesEvent(EntriesEvent.SET_SELECTED_ENTRIES_FOR_CATEGORY);
				cgEvent.dispatch();
				
				cgEvent = new CategoryEvent(CategoryEvent.SET_SELECTED_CATEGORY);
				cgEvent.data = Categories.createCategoryObject();
				cgEvent.dispatch();
				
				cgEvent = new WindowEvent(WindowEvent.OPEN, WindowsStates.CATEGORY_DETAILS_WINDOW);
				cgEvent.dispatch();

			}



			/**
			 * Entry table dispatched an order-by changing
			 */
			private function onOrderByChanged():void {
				_orderBy = entryTable.orderBy;
				loadEntries();
			}


			private function changeRows():void {
				GoogleAnalyticsTracker.getInstance().sendToGA(GoogleAnalyticsConsts.GO_TO_PAGE, GoogleAnalyticsConsts.CONTENT);
				VAnalyticsTracker.getInstance().sendEvent(VAnalyticsTrackerConsts.CONTENT, VidiunStatsVmcEventType.CONTENT_CONTENT_GO_TO_PAGE,
					"content>Show Rows");
				loadEntries();
			}


			private function nxtPage():void {
				GoogleAnalyticsTracker.getInstance().sendToGA(GoogleAnalyticsConsts.NEXT_PAGE, GoogleAnalyticsConsts.CONTENT);
				VAnalyticsTracker.getInstance().sendEvent(VAnalyticsTrackerConsts.CONTENT, VidiunStatsVmcEventType.CONTENT_CONTENT_GO_TO_PAGE,
					"content>NextPage/Page" + paging.selectedPage);
				loadEntries();
			}


			private function prvPage():void {
				GoogleAnalyticsTracker.getInstance().sendToGA(GoogleAnalyticsConsts.PREV_PAGE, GoogleAnalyticsConsts.CONTENT);
				VAnalyticsTracker.getInstance().sendEvent(VAnalyticsTrackerConsts.CONTENT, VidiunStatsVmcEventType.CONTENT_CONTENT_GO_TO_PAGE,
					"content>PrevPage/Page" + paging.selectedPage);
				loadEntries();
			}


			private function gotoPageNum():void {
				GoogleAnalyticsTracker.getInstance().sendToGA(GoogleAnalyticsConsts.GO_TO_PAGE_NUM, GoogleAnalyticsConsts.CONTENT);
				VAnalyticsTracker.getInstance().sendEvent(VAnalyticsTrackerConsts.CONTENT, VidiunStatsVmcEventType.CONTENT_CONTENT_GO_TO_PAGE,
					"content>GoToPage/Page" + paging.selectedPage);
				loadEntries();
			}


			protected function creationCompleteHandler(event:FlexEvent):void {
				PermissionManager.getInstance().applyAllAttributes(this, this.id);
				FileUploadManager.getInstance().addEventListener(FileUploadEvent.GROUP_UPLOAD_STARTED, onGroupUploadStarted);
			}


			/**
			 * if a replacement process begun, refresh the entries to be updated
			 * */
			private function onGroupUploadStarted(event:Event):void {
				//refresh only when entryDetailsWin is closed, to avoid null selected entry
				if ((windowState == WindowsStates.REPLACEMENT_ENTRY_DETAILS_WINDOW) || (windowState == WindowsStates.ENTRY_DETAILS_WINDOW_NEW_ENTRY) || (windowState == WindowsStates.ENTRY_DETAILS_WINDOW_CLOSED_ONE) || (windowState == WindowsStates.ENTRY_DETAILS_WINDOW)) {
					//refresh the open entry
					var edp:EntryDataPack = entryDetailsModel.getDataPack(EntryDataPack) as EntryDataPack;
					var getSelectedEntry:VedEntryEvent = new VedEntryEvent(VedEntryEvent.UPDATE_SELECTED_ENTRY_REPLACEMENT_STATUS, null, edp.selectedEntry.id);
					VedController.getInstance().dispatch(getSelectedEntry);
				}
				else
					// this is why we get a million baseEntry.list calls when uploading multiple files!!!
					// fixed by interval in loadEntries()
					loadEntries();
			}

		]]>
	</mx:Script>
	<business:EntryTableActionsManager id="actionsManager" />
	<mx:HBox width="100%" verticalAlign="middle">
		<mx:Label id="pageLabel" text="{resourceManager.getString('cms', 'entriesTitle')}" styleName="filterTitleHeader" 
				  width="{filter.width}"/>
		<fltr:FreeTextSearch id="searchBox" styleName="searchHBox" width="{245}" height="{pageLabel.height}"
							 attribute="freeText" defaultText="{resourceManager.getString('filter', 'searchEntries')}"
							 helpAnchore="freetext_entries" />
		<indicators:Indicators id="indicators" width="100%" height="60"
							   dataProvider="{filter.indicators}"
							   boxClicked="filter.removeFilter(event.data)"/>
		<mx:VBox styleName="noPadding">
			<mx:LinkButton click="loadEntries()" label="{resourceManager.getString('cms','refresh')}"
						   styleName="recycleIcon"/>
			<mx:Label text="{resourceManager.getString('filter', 'indicatorsLabel', [indicators.numExceeded])}" 
					  visible="{indicators.numExceeded > 0}" styleName="indicatorsLabel"/>
		</mx:VBox>
			
	</mx:HBox>
	<!--<mx:Spacer height="15" />-->
	<mx:HDividedBox id="topContainer" width="100%" height="100%">
		<fltr:EntriesFilter id="filter" height="100%" width="245" 
							freeTextSearch="{searchBox}" metadataProfiles="{filterModel.metadataProfiles}"
							context="{entryDetailsModel.getDataPack(ContextDataPack) as ContextDataPack}"
							distributionProfiles="{entryDetailsModel.getDataPack(DistributionDataPack)['distributionInfo'].distributionProfiles}"
							filterModel="{filterModel}" categoriesTreeSelectionAttribute="selectedEntriesScreen"
							categoriesRoot="{filterModel.categoriesForEntries}" allCategories="{filterModel.categoriesMapForEntries}"
							filterChanged="newSearchFromFilter()"/>

		<mx:VBox id="entryTableContainer" height="100%" width="100%" styleName="entriesTablePagerAndButtonsVbox">
			<et:EntryTable id="entryTable" width="100%" height="100%"
						   selectionChanged="actionsManager.itemClickHandler(event)"
						   showDetails="actionsManager.showEntryDetailsHandler(event)" 
						   openPreview="actionsManager.preview(event)"
						   deleteEntry="actionsManager.deleteEntries(event)"
						   openLiveDashboard="actionsManager.showLiveDashboard(event)"
						   dataProvider="{_listableVo.arrayCollection}" 
						   creationComplete="removeColums(event)"
						   selectedEntry="{(entryDetailsModel.getDataPack(EntryDataPack) as EntryDataPack).selectedEntry}"
						   showEmbed="{showEmbed}" orderByChanged="{onOrderByChanged()}"/>

			<mx:HBox width="100%" styleName="noPadding" horizontalGap="0">
				
				<bulkactions:BulkEntryActionsMenu id="bulkActions" height="{paging.height}"
											 createNewManualPlaylist="onNewManualPlaylist()"
											 createNewCategory="onNewCategory()"/>
				
				<controls:Paging id="paging" width="100%" styleName="paging"
								 rowsInPageChange="changeRows()" nextPage="{nxtPage()}" prvPage="{prvPage()}"
								 getPageNum="gotoPageNum()" showRowsInPage="true" defaultPageSize="50"/>
			</mx:HBox>
		</mx:VBox>
	</mx:HDividedBox>
</mx:VBox>
