<?xml version="1.0" encoding="utf-8"?>
<!-- change entry owner window -->
<containers:HelpTitleWindow xmlns:mx="http://www.adobe.com/2006/mxml" xmlns:containers="com.vidiun.containers.*"
							height="170" width="400" layout="vertical" showCloseButton="true" showHelpButton="true"
							help="helpHandler(event)"
							title="{resourceManager.getString('drilldown', 'changeOwnerTitle')}"
							creationComplete="creationCompleteHandler(event)"
							xmlns:components="com.hillelcoren.components.*">
	<mx:Metadata>
		/**
		 * dispatched when user requests save
		 * */
		[Event(name="save", type="fl.events.Event")]
	</mx:Metadata>

	<mx:Script>
		<![CDATA[
			import com.vidiun.VidiunClient;
			import com.vidiun.autocomplete.controllers.VACUsersController;
			import com.vidiun.autocomplete.itemRenderers.selection.UserSelectedItem;
			import com.vidiun.edw.control.VedController;
			import com.vidiun.edw.control.events.VedEntryEvent;
			import com.vidiun.edw.control.events.UsersEvent;
			import com.vidiun.edw.events.GeneralNonCairngormEvent;
			import com.vidiun.edw.model.datapacks.EntryDataPack;
			import com.vidiun.edw.view.EntryDetailsWin;
			import com.vidiun.vo.VidiunUser;

			import mx.binding.utils.BindingUtils;
			import mx.binding.utils.ChangeWatcher;
			import mx.collections.ArrayCollection;
			import mx.events.CloseEvent;
			import mx.events.FlexEvent;
			import mx.managers.PopUpManager;

			public var entryData:EntryDataPack;


			/**
			 * client for autocomplete API calls
			 * */
			public var vClient:VidiunClient

			private var _userWatcher:ChangeWatcher;


			private function creationCompleteHandler(event:FlexEvent):void {
				new VACUsersController(usersComplete, vClient);
				if (_selectedUser) {
					selectedUser = _selectedUser;
				}
			}


			/**
			 * container for selected user if not creationComplete yet
			 * */
			private var _selectedUser:VidiunUser


			/**
			 * retreive the currently selected user
			 * */
			public function get selectedUser():VidiunUser {
				return usersComplete.selectedItem as VidiunUser;
			}


			public function set selectedUser(value:VidiunUser):void {
				if (usersComplete) {
					usersComplete.selectedItems = new ArrayCollection([value]);
				}
				else {
					_selectedUser = value;
				}
			}


			public function setUserById(userid:String):void {
				_userWatcher = BindingUtils.bindProperty(this, "selecteduser", entryData, "selectedEntryOwner");
				var vEvent:UsersEvent = new UsersEvent(UsersEvent.GET_ENTRY_OWNER);
				vEvent.data = userid;
				VedController.getInstance().dispatch(vEvent);
			}


			private function closeWin(event:MouseEvent):void {
				if (_userWatcher) {
					_userWatcher.unwatch();
				}
				dispatchEvent(new CloseEvent(CloseEvent.CLOSE));
			}


			private function apply(event:MouseEvent):void {
				dispatchEvent(new Event("save"));
				closeWin(null);
			}


			private function helpHandler(event:Event):void {
				var e:GeneralNonCairngormEvent = new GeneralNonCairngormEvent(EntryDetailsWin.HELP_REQUEST, true);
				e.data = 'section_entry_owner'
				dispatchEvent(e);

			}
		]]>
	</mx:Script>
	<mx:Label text="{resourceManager.getString('drilldown', 'changeOwnerText')}" width="100%"/>
	<components:AutoComplete id="usersComplete" width="100%" allowNewValues="true" labelField="id" showRemoveIcon="true"
							 selectionItemRendererClassFactory="{new ClassFactory(UserSelectedItem)}"
							 selectedItemStyleName="selectionBox"
							 unregisteredSelectedItemStyleName="unregisteredSelectionBox"
							 prompt="{resourceManager.getString('drilldown', 'changeOwnerPrompt')}"/>

	<mx:ControlBar width="100%" horizontalAlign="right">
		<mx:Button label="{resourceManager.getString('drilldown', 'cancel')}" styleName="cancelBtnStyle"
				   click="closeWin(event)"/>
		<mx:VRule height="25"/>
		<mx:Button label="{resourceManager.getString('drilldown', 'save')}" styleName="saveBtnStyle"
				   click="apply(event)" enabled="{usersComplete.selectedItem != null}"/>
	</mx:ControlBar>
</containers:HelpTitleWindow>
