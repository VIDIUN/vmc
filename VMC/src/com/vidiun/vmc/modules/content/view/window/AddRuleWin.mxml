<?xml version="1.0" encoding="utf-8"?>
<!---
	 on rule based playlists, this is the window that allows adding a new rule.
	 opened from <code>RulePlaylistWindow</code>
-->
<HelpTitleWindow xmlns:mx="http://www.adobe.com/2006/mxml" xmlns="com.vidiun.containers.*"
				 xmlns:window="com.vidiun.vmc.modules.content.view.window.*" 
				 showCloseButton="true" close="onClose()" help="{onHelp()}" 
				 implements="com.vidiun.edw.business.IDataOwner"
				 creationComplete="loadFilterData()" height="540" width="780" >
	<mx:Metadata>
		[Event (name="removeMe", type="flash.events.Event")]
	</mx:Metadata>

	<mx:Script>
		<![CDATA[
			import com.vidiun.edw.business.IDataOwner;
			import com.vidiun.edw.business.permissions.PermissionManager;
			import com.vidiun.edw.control.VedController;
			import com.vidiun.edw.control.events.LoadEvent;
			import com.vidiun.edw.model.FilterModel;
			import com.vidiun.vmc.events.VmcHelpEvent;
			import com.vidiun.vmc.modules.content.events.NewFilterEvent;
			import com.vidiun.vmc.modules.content.model.CmsModelLocator;
			import com.vidiun.vo.VidiunMediaEntryFilterForPlaylist;


			/**
			 * defines the value of the type property of the "removeMe" event
			 * */
			public static const REMOVE_ME:String = "removeMe";

			/**
			 * in case of editing an existing filter (rule), this is it.
			 * */
			public var filterForSingleRule:VidiunMediaEntryFilterForPlaylist;

			[Bindable]
			/**
			 * data (model) for filter
			 * */
			public var filterData:FilterModel;

			[Bindable]
			/**
			 * a list of distibution profiles for the filter
			 * */
			public var distributionProfilesArr : Array;


			/**
			 * dispatch a help event with relevant section data.
			 * */
			private function onHelp():void {
				dispatchEvent(new VmcHelpEvent(VmcHelpEvent.HELP, 'section_edit_rule'));
			}

			/**
			 * triggered by command after filter data is loaded
			 * */
			public function onRequestedDataLoaded():void {
				initWindow();
			}

			private function loadFilterData():void {
				var fe:LoadEvent = new LoadEvent(LoadEvent.LOAD_FILTER_DATA, this, filterData);
				VedController.getInstance().dispatch(fe);
			}

			private function initWindow():void {
				if (filterForSingleRule) {
					singleRule.ruleToEdit = filterForSingleRule;
				}
				else {
					singleRule.ruleToEdit = new VidiunMediaEntryFilterForPlaylist();
				}
				//async call to the vidiun server to run the rule filter 
				singleRule.runRule();
				PermissionManager.getInstance().applyAllAttributes(this, "addRuleWin");
			}


			/**
			 * close this popup - no changes
			 */
			private function onClose():void {
				singleRule.clear();
				dispatchEvent(new Event(REMOVE_ME));
			}


			/**
			 * apply this filter and close the popup
			 */
			private function save():void {
				var vidiunPlaylistFilter:VidiunMediaEntryFilterForPlaylist = (singleRule.ruleFilter);
				dispatchEvent(new NewFilterEvent(NewFilterEvent.NEW_PLAYLIST_FILTER, vidiunPlaylistFilter));
				onClose();
			}
		]]>
	</mx:Script>
	<mx:HBox width="100%" height="100%">
		<window:OneRule id="singleRule" width="100%" height="100%" filterData="{filterData}" 
						distributionProfilesArray="{distributionProfilesArr}" />
	</mx:HBox>
	<mx:ControlBar>
		<mx:Button id="saveBtn" label="{resourceManager.getString('cms', 'saveChanges')}" buttonMode="true" click="save()"/>
	</mx:ControlBar>
</HelpTitleWindow>
