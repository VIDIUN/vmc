<?xml version="1.0" encoding="utf-8"?>
<!---
	 graphic interface for changing player playlists (multiple playlist only).
-->
<mx:VBox xmlns:mx="http://www.adobe.com/2006/mxml" width="100%" height="100%" >
	<mx:Metadata>
		
		[Event(name="alertUser", type="flash.events.ErrorEvent")] 
		
		[Event(name="detailsChanged", type="flash.events.Event")]
		
		/**
		 * Dispatched when playlists are loaded
		 * */
		[Event(name="listPlaylistDone", type="flash.events.Event")]
	</mx:Metadata>
	
	<mx:Script>
		<![CDATA[
			import com.vidiun.commands.playlist.PlaylistList;
			import com.vidiun.events.VidiunEvent;
			import com.vidiun.vmc.modules.studio.model.types.PlaylistTypes;
			import com.vidiun.vmc.modules.studio.view.ApsWizard;
			import com.vidiun.vmc.modules.studio.vo.PlaylistVO;
			import com.vidiun.types.VidiunPlaylistOrderBy;
			import com.vidiun.vo.VidiunFilterPager;
			import com.vidiun.vo.VidiunMediaEntryFilterForPlaylist;
			import com.vidiun.vo.VidiunPlaylist;
			import com.vidiun.vo.VidiunPlaylistFilter;
			import com.vidiun.vo.VidiunPlaylistListResponse;
			
			import modules.Studio;
			
			import mx.collections.ArrayCollection;
			import mx.formatters.DateFormatter;
			import mx.resources.ResourceManager;

			/**
			 * Defines the "type" property for the "listPlaylistDone" event
			 * */
			public static const LIST_PLAYLIST_DONE:String = "listPlaylistDone";

			public var isSingleContent:Boolean = false;

			[Bindable]
			/**
			 * DataProvider for the "all playlists" table
			 * */
			private var _playlistsTableDP:ArrayCollection = new ArrayCollection();
			
			[Bindable]
			/**
			 * DataProvider for the "selected playlists" table
			 * */
			private var _widgetPlstDP:ArrayCollection = new ArrayCollection();

			private var _contentXml:XML;


			/**
			 * validate data in this panel: </br>
			 * - no data is being validated. But someday..
			 * @return true if all valid, false otherwise.
			 * */
			public function validate():Boolean {
				return true;
			}


			//Handle List Playlist
			////////////////////////////////////////////
			
			/**
			 * get a list of all the partner's playlists
			 * */
			public function listPlaylists():void {
				var pager:VidiunFilterPager = new VidiunFilterPager();
				pager.pageIndex = 1;
				pager.pageSize = 500;	// large number, should get all
				var vplfilter:VidiunPlaylistFilter = new VidiunPlaylistFilter();
				vplfilter.orderBy = VidiunPlaylistOrderBy.CREATED_AT_DESC;
				var listPlaylist:PlaylistList = new PlaylistList(vplfilter, pager);
				listPlaylist.addEventListener(VidiunEvent.COMPLETE, onPlaylistsLoaded);
				listPlaylist.addEventListener(VidiunEvent.FAILED, fault);
				Studio.vc.post(listPlaylist);
			}


			/**
			 * All playlists were loaded - place them on the left side table
			 */
			private function onPlaylistsLoaded(event:VidiunEvent):void {
				var vmplf:VidiunMediaEntryFilterForPlaylist;

				var arr:Array = new Array();
				var plstVo:PlaylistVO;
				// create a VO for each playlist and it them to dataProvider
				// event.data is VidiunPlaylistListResponse
				for each (var playlist:VidiunPlaylist in event.data.objects) {
					plstVo = new PlaylistVO();
					plstVo.entryId = playlist.id;
					plstVo.name = playlist.name.split("'").join("Â´");
					plstVo.mediaType = playlist.playlistType.toString();
					var playlistUrl:String = Studio.flashvars.protocol ? Studio.flashvars.protocol : "http://"; 
					playlistUrl += Studio.flashvars.host + "/index.php/partnerservices2/executeplaylist?playlist_id=" + playlist.id + "&partner_id=" + playlist.partnerId + "&subp_id=" + (Number(playlist.partnerId) * 100).toString() + "&format=8" + "&vs={vs}";
					plstVo.playlistUrl = encodeURIComponent(playlistUrl);
					plstVo.duration = playlist.duration;
					plstVo.createdAt = playlist.createdAt;
					arr.push(plstVo);
				}
				_playlistsTableDP = new ArrayCollection(arr);
				dispatchEvent(new Event(LIST_PLAYLIST_DONE));
			}


			private function fault(event:VidiunEvent):void {
				var s:String = "Content fault: \n" + event.error.errorMsg;
				dispatchEvent(new ErrorEvent(ApsWizard.ALERT_USER, false, false, s));
			}



			//Group Playlist Manipulation
			////////////////////////////////////////////
			private function addPlaylistItem():void {
				if (playlistsTable.selectedItem) {
					var oldPos:int = widgetPlst.verticalScrollPosition;
					var newPlst:PlaylistVO = (playlistsTable.selectedItem as PlaylistVO).clone() as PlaylistVO;

					if (isSingleContent && _widgetPlstDP.length >= 1) {
						var s:String = ResourceManager.getInstance().getString('aps', 'onlyOnePlaylist');
						dispatchEvent(new ErrorEvent(ApsWizard.ALERT_USER, false, false, s));
						return;
					}

					_widgetPlstDP.addItem(newPlst);
					widgetPlst.selectedIndex = oldPos;

					if (oldPos > 0)
						widgetPlst.scrollToIndex(oldPos);


					dispatchEvent(new Event("detailsChanged"));
				}

			}


			private function removePlaylistItem():void {
				if (widgetPlst.selectedItem) {
					var oldPos:int = widgetPlst.verticalScrollPosition;
					try {
						_widgetPlstDP.removeItemAt(widgetPlst.selectedIndex);
					}
					catch (e:Error) {
					}

					widgetPlst.selectedIndex = oldPos;

					if (oldPos > 0)
						widgetPlst.scrollToIndex(oldPos);


					dispatchEvent(new Event("detailsChanged"));
				}
			}


			private function movePlstItemUp():void {
				if (widgetPlst.selectedItem) {
					if (widgetPlst.selectedIndex > 0) {
						var newIndex:int = widgetPlst.selectedIndex - 1;
						var item:Object = _widgetPlstDP.source.splice(widgetPlst.selectedIndex, 1)[0];
						_widgetPlstDP.source.splice(newIndex, 0, item);
						widgetPlst.selectedItem = item;

						if (widgetPlst.selectedIndex - 1 > 0)
							widgetPlst.scrollToIndex(widgetPlst.selectedIndex - 1);

						dispatchEvent(new Event("detailsChanged"));
					}
				}
			}


			private function movePlstItemDown():void {
				if (widgetPlst.selectedItem) {
					if (widgetPlst.selectedIndex < widgetPlst.dataProvider.length) {
						var newIndex:int = widgetPlst.selectedIndex + 1;
						var item:Object = _widgetPlstDP.source.splice(widgetPlst.selectedIndex, 1)[0];
						_widgetPlstDP.source.splice(newIndex, 0, item);

						widgetPlst.selectedItem = item;

						if (widgetPlst.selectedIndex + 1 > 0)
							widgetPlst.scrollToIndex(widgetPlst.selectedIndex + 1);

						dispatchEvent(new Event("detailsChanged"));
					}
				}
			}



			/**
			 * label function for media type column
			 * */
			private function getCreateAtAsString(item:Object, column:DataGridColumn):String {
				return dateFormatter.format(new Date(int(item.createdAt) * 1000));
			}


			/**
			 * label function for media type column
			 * */
			private function getPlaylistMediaTypes(item:Object, column:DataGridColumn):String {
				var res:String = '';
				switch (item.mediaType) {
					case PlaylistTypes.STATIC:
						res = resourceManager.getString('aps', 'plTypeManual');
						break;
					case PlaylistTypes.DYNAMIC:
						res = resourceManager.getString('aps', 'plTypeDynamic');
						break;
					case PlaylistTypes.EXTERNAL:
						res = resourceManager.getString('aps', 'plTypeExternal');
						break;
				}

				return res;
			}



			/**
			 * generate XML representation for currently selected playlists
			 * @return XML
			 * */
			public function getWidgetPlaylistXML():XML {
				var plg:XML = <playlistGroup />;
				var pl:XML;
				for (var i:int = 0; i < _widgetPlstDP.length; i++) {
					pl = <var />;
					pl.@key = "playlistAPI.vpl" + i + "Id";
					pl.@value = (_widgetPlstDP[i] as PlaylistVO).entryId;
					plg.appendChild(pl);
						
					pl = <var />;
					pl.@key = "playlistAPI.vpl" + i + "Url";
					pl.@value = (_widgetPlstDP[i] as PlaylistVO).playlistUrl;
					plg.appendChild(pl);
						
					pl = <var />;
					pl.@key = "playlistAPI.vpl" + i + "Name";
					pl.@value = (_widgetPlstDP[i] as PlaylistVO).name;
					plg.appendChild(pl);
					
				}
				return plg;
			}


			/**
			 * set selected lists
			 * @param xml	representation of required playlists
			 * */
			public function setWidgetPlaylistXML(xml:XML):void {
				_widgetPlstDP.source = new Array();
				_contentXml = xml;
				var j:int = 0;
				for each (var prop:XML in xml.children()) {
					// Assumption that all the playlists are in _playlistsTableDP (no paging)!!
					// change in player plugin API - take playlist entry ID if possible
					if (prop.@key == "vpl" + j + "EntryId" || prop.@key == "playlistAPI.vpl" + j + "Id") {
						addPlaylistByEntryId(prop.@value);
						++j;
					}
				}
			}


			private function addPlaylistByEntryId(entryId:String):void {
				for (var i:int = 0; i < _playlistsTableDP.length; i++) {
					if ((_playlistsTableDP[i] as PlaylistVO).entryId == entryId) {
						_widgetPlstDP.addItem((_playlistsTableDP[i] as PlaylistVO).clone());
					}
				}
			}
		]]>
	</mx:Script>
	
	<mx:DateFormatter id="dateFormatter" formatString="{resourceManager.getString('aps', 'plCreateFormatString')}" />

	<mx:HBox id="all" disabledOverlayAlpha="0" height="100%" width="100%">
		<mx:VBox width="100%" height="100%">
			<mx:Label text="{resourceManager.getString('aps', 'allPlaylists')}"/>
			
			<mx:VBox styleName="noVGap" width="100%" height="100%" >
				<mx:DataGrid id="playlistsTable" width="100%" height="100%" styleName="borderedDataGrid" rowHeight="36" verticalAlign="middle"
							 draggableColumns="false" sortableColumns="false" doubleClickEnabled="true"
							 itemDoubleClick="{addPlaylistItem()}" dataProvider="{_playlistsTableDP}">
					<mx:columns>
						<mx:DataGridColumn paddingLeft="4" paddingRight="4" dataField="entryId" width="80"
										   headerText="{resourceManager.getString('aps', 'plIdHeader')}"/>
						
						<mx:DataGridColumn paddingLeft="4" paddingRight="4" dataField="name"
										   headerText="{resourceManager.getString('aps', 'plNameHeader')}"/>
						
						<mx:DataGridColumn paddingLeft="4" paddingRight="4" dataField="mediaType" width="80" 
										   headerText="{resourceManager.getString('aps', 'plTypeHeader')}" 
										   labelFunction="getPlaylistMediaTypes" />
							
						<mx:DataGridColumn paddingLeft="4" paddingRight="4" labelFunction="getCreateAtAsString" width="140"
										   headerText="{resourceManager.getString('aps', 'plCreateHeader')}"/>
					</mx:columns>
				</mx:DataGrid>
				<mx:HBox height="{widgetPlstToolbar.height}" width="100%" styleName="DataGridActions" />
			</mx:VBox>
		</mx:VBox>
		
		<mx:HBox height="100%" styleName="plstAddRemoveCont" >
			<mx:Button id="removeBtn" buttonMode="true" styleName="removeLeftBtn" click="removePlaylistItem()"/>
			<mx:Button id="addBtn" buttonMode="true" styleName="addRightBtn" click="addPlaylistItem()"/>
		</mx:HBox>
		
		<mx:VBox width="100%" height="100%" >
			<mx:Label text="{resourceManager.getString('aps', 'selectedPlaylists')}"/>
			<mx:VBox styleName="noVGap" width="100%" height="100%" >
				<mx:DataGrid id="widgetPlst" width="100%" height="100%" styleName="borderedDataGrid" rowHeight="36" verticalAlign="middle"
							 sortableColumns="false" doubleClickEnabled="true" itemDoubleClick="{removePlaylistItem()}"
							 dataProvider="{_widgetPlstDP}">
					<mx:columns>
						<mx:DataGridColumn paddingLeft="4" paddingRight="4" dataField="entryId" width="80"
										   headerText="{resourceManager.getString('aps', 'plIdHeader')}"/>
						
						<mx:DataGridColumn paddingLeft="4" paddingRight="4" dataField="name"
										   headerText="{resourceManager.getString('aps', 'plNameHeader')}"/>
						
						<mx:DataGridColumn paddingLeft="4" paddingRight="4" dataField="mediaType" width="80" 
										   headerText="{resourceManager.getString('aps', 'plTypeHeader')}" 
										   labelFunction="getPlaylistMediaTypes" />
						
						<mx:DataGridColumn paddingLeft="4" paddingRight="4" labelFunction="getCreateAtAsString" width="140"
										   headerText="{resourceManager.getString('aps', 'plCreateHeader')}"/>
					</mx:columns>
				</mx:DataGrid>
				
				<mx:VBox id="widgetPlstToolbar" width="100%" styleName="DataGridActions" verticalGap="0">
					<mx:Button id="upBtn" buttonMode="true" styleName="movUpBtn" click="movePlstItemUp()"/>
					<mx:Button id="downBtn" buttonMode="true" styleName="movDownBtn" click="movePlstItemDown()"/>
				</mx:VBox>
			</mx:VBox>
				
		</mx:VBox>
	</mx:HBox>

	<mx:VBox width="100%" styleName="plstContentDtlsCont" >
		<mx:Label text="{resourceManager.getString('aps', 'totalPlaylists', [_widgetPlstDP.length])}" />
	</mx:VBox>
</mx:VBox>
