<?xml version="1.0" encoding="utf-8"?>
<mx:VBox xmlns:mx="http://www.adobe.com/2006/mxml" xmlns:live="com.vidiun.edw.view.live.*">
	<mx:Script>
		<![CDATA[
			import com.vidiun.edw.business.EntryLiveStreamingHelper;
			import com.vidiun.types.VidiunSourceType;
			import com.vidiun.vo.VidiunLiveStreamConfiguration;
			import com.vidiun.vo.VidiunLiveStreamEntry;
			
			
			[Bindable]
			private var _selectedEntry:VidiunLiveStreamEntry;

			public function get selectedEntry():VidiunLiveStreamEntry
			{
				return _selectedEntry;
			}

			public function set selectedEntry(value:VidiunLiveStreamEntry):void
			{
				_selectedEntry = value;
				
				
				// clear any previous values (don't remove hlsStreamUrl):
				while (numChildren > 1) {
					removeChildAt(numChildren-1);
				}
				if (_selectedEntry.sourceType == VidiunSourceType.MANUAL_LIVE_STREAM) {
					populateManualBox(_selectedEntry.liveStreamConfigurations);
				}
			}
			
			
			/**
			 * add lines for each stream url
			 * @param cfgs	list of VidiunLiveStreamConfiguration to present 
			 * */
			private function populateManualBox(cfgs:Array):void
			{
				var line:FormLine;
				var i:int;
				
				// create all lines
				for each (var cfg:VidiunLiveStreamConfiguration in cfgs) {
					line = new FormLine();
					//line.prefixesWidth = 160; 
					// allow protocol name override by locale:
					line.label = resourceManager.getString('drilldown', cfg.protocol) ? resourceManager.getString('drilldown', cfg.protocol) : cfg.protocol
					line.label += resourceManager.getString('drilldown', 'streamUrl');
					line.value = cfg.url;
					addChild(line);
				}
				validateNow();
				
				// get max line width
				var maxLength:int = 0;
				for (i = 0; i<numChildren; i++) {
					line = getChildAt(i) as FormLine;
					if (!line) continue;
					maxLength = Math.max(maxLength, line.prefixWidth); 
				}
				
				// set to all lines
				for (i = 0; i<numChildren; i++) {
					line = getChildAt(i) as FormLine;
					if (!line) continue;
					line.prefixWidth = maxLength; 
				}
			}

		]]>
	</mx:Script>
	<live:FormLine prefixWidth="{EntryLiveStreamingHelper.PREFIXES_WIDTH}" 
				   label="{resourceManager.getString('drilldown', 'hlsStreamUrl')}" value="{ _selectedEntry.hlsStreamUrl}" />
</mx:VBox>
