<?xml version="1.0" encoding="utf-8"?>
<mx:HBox xmlns:mx="http://www.adobe.com/2006/mxml" verticalAlign="middle" horizontalScrollPolicy="off" >
	<mx:Script>
		<![CDATA[
			import com.vidiun.edw.view.window.flavors.FilesTable;
			import com.vidiun.edw.vo.UploadFileVo;
			import com.vidiun.vo.VidiunConversionProfileAssetParams;
			
			import mx.events.ListEvent;
			import mx.resources.ResourceManager;


			/**
			 * Object of type <code>UploadFileVo</code>
			 * */
			override public function set data(value:Object):void {
				super.data = value;
				if (value) {
//					cb.enabled = true;
					cb.dataProvider = value.flavors; // VidiunFlavorParams objects
					if (cb.dropdown) {
						cb.dropdown.dataProvider = value.flavors;
					}
					selectFlavor(value as UploadFileVo);
				}
			}


			/**
			 * for when changing only a single flavor (?)
			 * show the correct flavorParamId in the CB.
			 * otherwise use the file name pattern to "guess" flavor.
			 * */
			protected function selectFlavor(ufv:UploadFileVo):void {
				var found:Boolean;
				var o:VidiunConversionProfileAssetParams;
				
				if (!ufv.flavors || ufv.flavors.length == 0) {
					cb.prompt = ResourceManager.getInstance().getString('cms', 'noFlavorsPrompt');
					cb.enabled = false;
				}
				else {
					cb.enabled = true;
					// if a flavor was defined for this file (vo)
					for each(o in ufv.flavors) {
						if (o.assetParamsId.toString() == ufv.flavorParamId) {
							cb.selectedItem = o;
							found = true;
							break;
						}
					}
					// if no flavor was defined, try matching by flavor system name
					if (!found) {
						// use the flavor system name and file name to "guess" flavor.
						for each(o in ufv.flavors) {
							if (ufv.fileName && ufv.fileName.indexOf(o.systemName) != -1) {
								// in link, import statesufv.fileName is null.
								cb.selectedItem = o;
								ufv.flavorParamId = o.assetParamsId.toString();
								found = true;
								break;
							}
						}
					}
					// if that failed too, prompt "select".
					if (!found) {
						ufv.flavorParamId = '';
						cb.prompt = ResourceManager.getInstance().getString('cms', 'selectFlavorPrompt');
	//					cb.enabled = false;
					}
				}
				
			}


			protected function combobox1_changeHandler(event:ListEvent):void {
				dispatchEvent(new Event(FilesTable.FLAVOR_SELECTED, true));
			}


			public function get selectedFlavor():VidiunConversionProfileAssetParams {
				return cb.selectedItem as VidiunConversionProfileAssetParams;
			}

//			public function set selectedFlavor(value:VidiunFlavorParams):void {
//				cb.selectedItem = value;
//			}
		]]>
	</mx:Script>

	<mx:ComboBox id="cb" itemRenderer="com.vidiun.edw.view.window.flavors.ir.CheckBoxLabelRenderer" 
				 change="combobox1_changeHandler(event)" labelField="name" width="100%"/>
</mx:HBox>
